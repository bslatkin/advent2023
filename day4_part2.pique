import "advent"
import "dict"
import "functional"
import "list"
import "set"
import "sort"
import "string"


def parse-card (card)
  let number-text = substr card (strlen "Card") (strlen card)
  let stripped = string.strip number-text
  let number = string.string-to-integer stripped
  return number


def parse-numbers (line)
  let numbers-text = string.split line " "
  let numbers =
    map
      string.string-to-integer
      numbers-text
  let filtered =
    filter
      lambda x: not-equal x nil
      numbers
  print "numbers are" filtered
  let numbers-set = set.from-list filtered
  print "numbers" numbers-set
  return numbers-set


def match-numbers (winning have)
  let overlap = set.intersection winning have
  print "overlap" overlap
  return set.values overlap


def handle-line (line)
  let before have = string.split line " | "
  let card winning = string.split before ": "
  let card-number = parse-card card
  let have-numbers = parse-numbers have
  let winning-numbers = parse-numbers winning
  let matching = match-numbers winning-numbers have-numbers
  let wins = list.length matching
  return list card-number wins


def additional-wins (card-index extra-index card-number)
  let extra-count = dict.get-or-default extra-index card-number nil
  if not-equal extra-count nil then
    return extra-count

  # Instructions say the card number will never be out of range, but this
  # isn't true for the tiny test data.
  let wins = dict.get-or-default card-index card-number 0

  if not wins then
    return 0

  let extra-card-numbers =
    map
      lambda i: add card-number (add i 1)
      range wins

  print "extra cards from" card-number "with" wins "wins:" extra-card-numbers

  if not extra-card-numbers then
    return 0

  let transitive-extra-cards =
    map
      functional.partial additional-wins card-index extra-index
      extra-card-numbers

  print "transitive cards from" card-number "with" wins "wins:" transitive-extra-cards

  let extra-card-count = list.length extra-card-numbers

  if not transitive-extra-cards then
    return extra-card-count

  let flattened-count = sum transitive-extra-cards
  let all-cards-count = add extra-card-count flattened-count
  return all-cards-count


def fold-wins (card-index extra-index card-number)
  let card-wins = additional-wins card-index extra-index card-number
  let next-extra-index = dict.set extra-index card-number card-wins
  return next-extra-index



def main (args)
  let data = (advent.read-all)
  let card-scores = map handle-line data
  let card-index = dict.from-list card-scores

  # Count this in reverse order, and then cache already calculated
  # values so later look-ups will find the precomputed answer instead of
  # having to do it again? Dynamic programming type of solution.
  let sorted-cards = sort.sort-with-compare sort.descending (dict.keys card-index)

  let extra-cards-count =
    functional.fold
      functional.partial fold-wins card-index
      sorted-cards
      (dict.new)

  print extra-cards-count

  let total-extra = sum (dict.values extra-cards-count)
  let original = dict.size card-index
  let total = add original total-extra
  print total
  return 0
